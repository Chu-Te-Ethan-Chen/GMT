#!/bin/csh
# UPDATE: 2020/09/03, ctc
#
# Goals:
# 1. draw Azimuth v.s. Distance graph
# 
# Given:
# 1. a sample graph https://palert.earth.sinica.edu.tw/historyevent.php
# 2. TSMIP_PGAs_event.txt
# 
# Constraints: none
# 
# Developed:
# 1. draw Azimuth v.s. Distance graph
#
# Under developement:
# 1. defree symbol
#
# Notes:
# 1. recalculate Azimuth when drawing -180 to 180
#
# Debug: none
#
# Assumption: none
# 

# =====Input arguments===== #
if($#argv != 1) then
  echo
  echo "plotAZ2Dist.gmt [eventDir]"
  echo "-EX: plotAZ2Dist.gmt 20190807212803"
  echo "-It will draw AZ_Dist_PGA graph."
  echo
  exit 1
endif

# ====Preparation===== #
set fileDir = $1
set eventPgaInfo = `ls $fileDir/TSMIP_PGAs_$1.txt`
set fileName = `ls $fileDir/TSMIP_PGAs_$1.txt | awk -F'/' '{print $NF}'`
echo $eventPgaInfo
echo $fileName
set event = `echo $fileName | awk -F'_' '{print $3}' | awk -F'.' '{print $1}'`

# find event from catalog.dat
set catalog = 'catalog.dat'
set yr = `echo $event | cut -c 1-4`
set mm = `echo $event | cut -c 5-6`
set day = `echo $event | cut -c 7-8`
set hr = `echo $event | cut -c 9-10`
set min = `echo $event | cut -c 11-12`
set sec = `echo $event | cut -c 13-14`
set ymd = $yr-$mm-$day
set hms = $hr':'$min':'$sec
echo $event $ymd $hms

set ymdCatalog = `awk '{print $1}' $catalog`
set hmsCatalog = `awk '{print $2}' $catalog`
set lat = `awk '{print $3}' $catalog`
set lon = `awk '{print $4}' $catalog`
set dep = `awk '{print $5}' $catalog`
set ML = `awk '{print $6}' $catalog`
set i = 1
foreach line ( $ymdCatalog )
  set hmsStripedMS = `echo $hmsCatalog[$i] | cut -c 1-8`
  if ( $ymd == $ymdCatalog[$i] &&  $hms == $hmsStripedMS) then
    set evla = $lat[$i]
    set evlo = $lon[$i]
    set evdp = $dep[$i]
    set mag = $ML[$i]
#    echo $line $evla $evlo $evdp $mag
  endif
  @ i = $i + 1
end


# =====Draw===== #
set output = plot/AZ_Dist_PGA_$event'.ps'
set jpg = plot/AZ_Dist_PGA_$event'.jpg'
set cpt = PGA.cpt
rm -f $output
rm -f $jpg

psbasemap -JX6.472i/4i -R0/360/0/300 -Ba90f30/a100f25:"Distance (km)":WSne -P -K -V -Y18 > $output
awk '{print $8,$7,$9}' $eventPgaInfo | psxy -J -R -B -Sc0.2 -W1/black -C$cpt -K -O -V >> $output

psbasemap -JX6.472i/4i -R-180/180/0/300 -Ba90f30:"Azimuth":/a100f25:"Distance (km)":WSne -P -K -O -V -Y-12 >> $output
awk '{if($8 > 180){print $8-360,$7,$9} else{print $8,$7,$9}}' $eventPgaInfo | psxy -J -R -B -Sc0.2 -W1/black -C$cpt -K -O -V >> $output

# color bar
#psscale -C$cpt -D12.2/3.5/6/0.25 -B/:"PGA (gal)": -L -S -O >> $output
psscale -C$cpt -D7.5/-2.5/12/0.25h -B/:"PGA (gal)": -N -L -S -O >> $output

# Convert ps to jpg
echo 'converting ps to jpg'
convert -density 150 -trim $output $jpg
echo '--> done'

eog $jpg
#gv $output
